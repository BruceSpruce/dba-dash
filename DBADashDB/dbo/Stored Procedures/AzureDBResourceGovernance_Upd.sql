CREATE PROC dbo.AzureDBResourceGovernance_Upd(
	@AzureDBResourceGovernance dbo.AzureDBResourceGovernance READONLY,
	@InstanceID INT,
	@SnapshotDate DATETIME2(2)
)
AS
SET XACT_ABORT ON
DECLARE @Ref VARCHAR(30)='AzureDBResourceGovernance'
IF NOT EXISTS(SELECT 1 FROM dbo.CollectionDates WHERE SnapshotDate>=@SnapshotDate AND InstanceID = @InstanceID AND Reference=@Ref)
BEGIN
	BEGIN TRAN
	UPDATE RGH 
		SET RGH.ValidTo = @SnapshotDate
	FROM dbo.AzureDBResourceGovernanceHistory RGH
	WHERE RGH.InstanceID = @InstanceID
	AND RGH.ValidTo = '9999-12-31'
	AND EXISTS(SELECT database_id,
                      logical_database_guid,
                      physical_database_guid,
                      server_name,
                      database_name,
                      slo_name,
                      dtu_limit,
                      cpu_limit,
                      min_cpu,
                      max_cpu,
                      cap_cpu,
                      min_cores,
                      max_dop,
                      min_memory,
                      max_memory,
                      max_sessions,
                      max_memory_grant,
                      max_db_memory,
                      govern_background_io,
                      min_db_max_size_in_mb,
                      max_db_max_size_in_mb,
                      default_db_max_size_in_mb,
                      db_file_growth_in_mb,
                      initial_db_file_size_in_mb,
                      log_size_in_mb,
                      instance_cap_cpu,
                      instance_max_log_rate,
                      instance_max_worker_threads,
                      replica_type,
                      max_transaction_size,
                      checkpoint_rate_mbps,
                      checkpoint_rate_io,
                      last_updated_date_utc,
                      primary_group_id,
                      primary_group_max_workers,
                      primary_min_log_rate,
                      primary_max_log_rate,
                      primary_group_min_io,
                      primary_group_max_io,
                      primary_group_min_cpu,
                      primary_group_max_cpu,
                      primary_log_commit_fee,
                      primary_pool_max_workers,
                      pool_max_io,
                      govern_db_memory_in_resource_pool,
                      volume_local_iops,
                      volume_managed_xstore_iops,
                      volume_external_xstore_iops,
                      volume_type_local_iops,
                      volume_type_managed_xstore_iops,
                      volume_type_external_xstore_iops,
                      volume_pfs_iops,
                      volume_type_pfs_iops,
                      user_data_directory_space_quota_mb
				FROM @AzureDBResourceGovernance
				EXCEPT 
				SELECT database_id,
                      logical_database_guid,
                      physical_database_guid,
                      server_name,
                      database_name,
                      slo_name,
                      dtu_limit,
                      cpu_limit,
                      min_cpu,
                      max_cpu,
                      cap_cpu,
                      min_cores,
                      max_dop,
                      min_memory,
                      max_memory,
                      max_sessions,
                      max_memory_grant,
                      max_db_memory,
                      govern_background_io,
                      min_db_max_size_in_mb,
                      max_db_max_size_in_mb,
                      default_db_max_size_in_mb,
                      db_file_growth_in_mb,
                      initial_db_file_size_in_mb,
                      log_size_in_mb,
                      instance_cap_cpu,
                      instance_max_log_rate,
                      instance_max_worker_threads,
                      replica_type,
                      max_transaction_size,
                      checkpoint_rate_mbps,
                      checkpoint_rate_io,
                      last_updated_date_utc,
                      primary_group_id,
                      primary_group_max_workers,
                      primary_min_log_rate,
                      primary_max_log_rate,
                      primary_group_min_io,
                      primary_group_max_io,
                      primary_group_min_cpu,
                      primary_group_max_cpu,
                      primary_log_commit_fee,
                      primary_pool_max_workers,
                      pool_max_io,
                      govern_db_memory_in_resource_pool,
                      volume_local_iops,
                      volume_managed_xstore_iops,
                      volume_external_xstore_iops,
                      volume_type_local_iops,
                      volume_type_managed_xstore_iops,
                      volume_type_external_xstore_iops,
                      volume_pfs_iops,
                      volume_type_pfs_iops,
                      user_data_directory_space_quota_mb
			)
	INSERT INTO dbo.AzureDBResourceGovernanceHistory(
					  InstanceID,
					  ValidFrom,
					  ValidTo,
					  database_id,
                      logical_database_guid,
                      physical_database_guid,
                      server_name,
                      database_name,
                      slo_name,
                      dtu_limit,
                      cpu_limit,
                      min_cpu,
                      max_cpu,
                      cap_cpu,
                      min_cores,
                      max_dop,
                      min_memory,
                      max_memory,
                      max_sessions,
                      max_memory_grant,
                      max_db_memory,
                      govern_background_io,
                      min_db_max_size_in_mb,
                      max_db_max_size_in_mb,
                      default_db_max_size_in_mb,
                      db_file_growth_in_mb,
                      initial_db_file_size_in_mb,
                      log_size_in_mb,
                      instance_cap_cpu,
                      instance_max_log_rate,
                      instance_max_worker_threads,
                      replica_type,
                      max_transaction_size,
                      checkpoint_rate_mbps,
                      checkpoint_rate_io,
                      last_updated_date_utc,
                      primary_group_id,
                      primary_group_max_workers,
                      primary_min_log_rate,
                      primary_max_log_rate,
                      primary_group_min_io,
                      primary_group_max_io,
                      primary_group_min_cpu,
                      primary_group_max_cpu,
                      primary_log_commit_fee,
                      primary_pool_max_workers,
                      pool_max_io,
                      govern_db_memory_in_resource_pool,
                      volume_local_iops,
                      volume_managed_xstore_iops,
                      volume_external_xstore_iops,
                      volume_type_local_iops,
                      volume_type_managed_xstore_iops,
                      volume_type_external_xstore_iops,
                      volume_pfs_iops,
                      volume_type_pfs_iops,
                      user_data_directory_space_quota_mb,
                      user_data_directory_space_usage_mb
					  )

	SELECT @InstanceID,
		@SnapshotDate ValidFrom,
		'9999-12-31' AS ValidTo,
		database_id,
        logical_database_guid,
        physical_database_guid,
        server_name,
        database_name,
        slo_name,
        dtu_limit,
        cpu_limit,
        min_cpu,
        max_cpu,
        cap_cpu,
        min_cores,
        max_dop,
        min_memory,
        max_memory,
        max_sessions,
        max_memory_grant,
        max_db_memory,
        govern_background_io,
        min_db_max_size_in_mb,
        max_db_max_size_in_mb,
        default_db_max_size_in_mb,
        db_file_growth_in_mb,
        initial_db_file_size_in_mb,
        log_size_in_mb,
        instance_cap_cpu,
        instance_max_log_rate,
        instance_max_worker_threads,
        replica_type,
        max_transaction_size,
        checkpoint_rate_mbps,
        checkpoint_rate_io,
        last_updated_date_utc,
        primary_group_id,
        primary_group_max_workers,
        primary_min_log_rate,
        primary_max_log_rate,
        primary_group_min_io,
        primary_group_max_io,
        primary_group_min_cpu,
        primary_group_max_cpu,
        primary_log_commit_fee,
        primary_pool_max_workers,
        pool_max_io,
        govern_db_memory_in_resource_pool,
        volume_local_iops,
        volume_managed_xstore_iops,
        volume_external_xstore_iops,
        volume_type_local_iops,
        volume_type_managed_xstore_iops,
        volume_type_external_xstore_iops,
        volume_pfs_iops,
        volume_type_pfs_iops,
        user_data_directory_space_quota_mb,
        user_data_directory_space_usage_mb
	FROM @AzureDBResourceGovernance
	WHERE NOT EXISTS(SELECT 1 
					FROM dbo.AzureDBResourceGovernanceHistory RGH 
					WHERE RGH.InstanceID = @InstanceID 
					AND RGH.ValidTo = '9999-12-31'
					);

	/*  Update user_data_directory_space_usage_mb so we have current value but don't track as a ResourceGovernanceHistory change.  This value will be
		volatile and represents a change in usage rather than configuration
	   (Current local storage consumption by data files, transaction log files, and tempdb files, in MB. Updated every five minutes.)
	*/
	UPDATE RGH 
		SET RGH.user_data_directory_space_usage_mb=T.user_data_directory_space_usage_mb
	FROM dbo.AzureDBResourceGovernanceHistory RGH
	JOIN @AzureDBResourceGovernance T ON RGH.database_id = T.database_id
	WHERE RGH.InstanceID = @InstanceID
	AND RGH.ValidTo = '9999-12-31'

	EXEC dbo.CollectionDates_Upd @InstanceID = @InstanceID,  
										 @Reference = @Ref,
										 @SnapshotDate = @SnapshotDate;
	COMMIT
END
